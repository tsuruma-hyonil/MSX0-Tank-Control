10 'MSX0 TANK Control version 1.0.0
20 '(C)2023 h.Tsuruma
30 'License: MIT License
100 '****** Initialize ******
110 CLEAR 4000:DEFINT A-Z:SCREEN 1:CLS:KEY OFF:WIDTH 32
120 DIM MS(1,3):RESTORE 5100:FOR I=0 TO 1:FOR J=0 TO 3:READ MS(I,J):NEXT J:NEXT I
130 DIM MC$(1)
140 DIM MT(8,3):RESTORE 5200:FOR I=0 TO 8:FOR J=0 TO 3:READ MT(I,J):NEXT J:NEXT I
150 DIM SK$(8):RESTORE 5300:FOR I=0 TO 8:READ SK$(I):NEXT I:SK=-1
160 DN$="device/i2c_a"
170 DIM MA$(1):FOR I=0 TO 1:MA$(I)=DN$+"/"+HEX$(MS(I,0)):NEXT I
180 BM=0:DIM BM$(1):RESTORE 5400:FOR I=0 TO 1:READ BM$(I):NEXT I
190 '--- I2C device init ---
200 _IOTINIT():PRINT "Device search..."
210 DC=0:_IOTFIND(DN$,DC)
220 IF DC=0 THEN PRINT "PortA device not found.":END
230 DIM DA$(DC):_IOTFIND(DN$,DA$(0),DC)
240 FOR I=0 TO DC:FOR J=0 TO 1
250 IF HEX$(MS(J,0))=DA$(I) THEN MS(J,1)=1
260 NEXT J:NEXT I
270 IF MS(0,1)=0 THEN PRINT "DRV8830(Left) not found.":END
280 IF MS(1,1)=0 THEN PRINT "DRV8830(Right) not found.":END
290 '--- screen init ---
300 _TURBO ON
310 FOR I=&H21 TO &H7E:FOR J=0 TO 7:L=VPEEK(I*8+J):VPOKE I*8+J,(L OR (L\2)):NEXT J:NEXT I
320 LOCATE 0,0: PRINT "MSX0 TANK Control"
330 LOCATE 0,2: PRINT "DRV8830 status"
340 LOCATE 0,3: PRINT "LEFT":LOCATE 16,3:PRINT "RIGHT"
350 LOCATE 0,4: PRINT "CT:": LOCATE 16,4:PRINT "CT:"
360 LOCATE 0,5: PRINT "VS:": LOCATE 16,5:PRINT "VS:"
370 LOCATE 0,7: PRINT "MSX0 status"
380 LOCATE 0,8: PRINT "STICK:"
390 LOCATE 0,9: PRINT "ACCEL:"
400 LOCATE 0,10:PRINT "BGM:"
410 _TURBO OFF
420 '--- interval init ---
430 GOSUB 2100
440 ON INTERVAL=60 GOSUB 2000:INTERVAL ON
500 '****** Main loop ******
510 '--- invoke interval event ---
520 IF IV=1 THEN GOSUB 2100
530 '--- get key status ---
540 K$=INKEY$:
550 IF K$="m" AND BM=0 THEN BM=1:RESTORE 5500: 'play music
560 '--- get stick status, to motor control ---
570 S=STICK(0):IF S<>SK THEN SK=S:LOCATE 7,8:PRINT SK$(SK)+SPACE$(16-LEN(SK$(SK)));:GOSUB 1000
990 GOTO 500
1000 '****** Motor control ******
1010 '--- make command ---
1020 FOR I=0 TO 1
1030 MS(I,2)=MT(SK,I*2):MS(I,3)=MT(SK,I*2+1)
1040 MC$(I)=CHR$(0)+CHR$(MS(I,3)*4+MS(I,2))
1050 LOCATE I*16+4,4:C$=STR$(MS(I,2)):PRINT SPACE$(4-LEN(C$))+C$;
1060 LOCATE I*16+4,5:V$=STR$(MS(I,3)):PRINT SPACE$(4-LEN(V$))+V$;
1070 NEXT I
1080 '--- put command to DRV8830 ---
1090 _IOTPUT(MA$(0), MC$(0))
1100 _IOTPUT(MA$(1), MC$(1))
1990 RETURN
2000 '****** Interval flag on ******
2010 IV=1:RETURN
2100 '****** Interval event process ******
2110 '--- get accel ---
2120 _IOTGET("device/accel/x",X):_IOTGET("device/accel/y",Y):_IOTGET("device/accel/z",Z)
2130 LOCATE 7,9:A$="X="+STR$(X\10)+";Y="+STR$(Y\10)+";Z="+STR$(Z\100):PRINT A$+SPACE$(25-LEN(A$))
2140 '--- print BGM status ---
2150 LOCATE 5,10:PRINT BM$(BM)+SPACE$(8-LEN(BM$(BM)))
2160 '--- play music ---
2170 IF BM<>1 OR PEEK(&HFB40)<>0 THEN 2210
2180 READ M1$,M2$
2190 IF M1$="##" THEN BM=0:GOTO 2210
2200 PLAY M1$,M2$
2210 '
2990 IV=0:RETURN
5000 '****** Data ******
5100 '--- DRV8830 status table(ADDRESS,FOUND,CT,VS)
5110 DATA &H64,0,0,0: 'Left side
5120 DATA &H61,0,0,0: 'Right side
5200 '--- motor control table(CT[L],VS[L],CT[R],VS[R])
5210 DATA 3, 6,3, 6: 'Inactive
5220 DATA 1,38,1,38: 'Up
5230 DATA 1,38,1,16: 'Up Right
5240 DATA 1,38,1, 8: 'Right'
5250 DATA 2,38,2,16: 'Right Down'
5260 DATA 2,38,2,38: 'Down'
5270 DATA 2,16,2,38: 'Down Left'
5280 DATA 1, 8,1,38: 'Left'
5290 DATA 1,16,1,38: 'Up Left'
5300 '--- stick status text
5310 DATA "Inactive","Up","Up Right"
5320 DATA "Right","Right Down","Down"
5330 DATA "Down Left","Left","Up Left"
5400 '--- BGM status text
5410 DATA "Stop","Playing"
5500 '--- BGM(Panzerlied)
5510 DATA "T180O4L4GO5L2CO4E.L8F","T180O4L4GL2EE.L8F"
5520 DATA "L2EL4DCL2GO5L4E.L8D","L2EL4DCL2GL4G.L8F"
5530 DATA "L2CR4L4CL2DO4L4A.L8A","L2ER4L4EL2FL4F.L8F"
5540 DATA "L2AL4GFL2EL4GL8FL2ER4","L2FL4EDL2CO3L4BO4L8DL2CR4"
5550 DATA "O4L4GG.L8BO5L4DD","L4GG.L8BO5L4DD"
5560 DATA "DCO4GGO5DL8CL4DE","DCO4GGO5DL8CL4DE"
5570 DATA "L1FL4FR4R4O4G","FDO4BAGR4R4G"
5580 DATA "O5L2EL4C.O4L8BL2AO5L4D.L8C","L2GL4E.L8EL2FL4F.L8F"
5590 DATA "O4L2BO5L4E.L8DL2CR4","L2GL4G.L8FL2ER4","##","##"
